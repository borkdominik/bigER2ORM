grammar org.big.orm.language.JavaModel with org.eclipse.xtext.common.Terminals

generate javaModel "http://www.big.org/orm/language/java/JavaModel"

JavaModel:
	("package" package=DOT_ID";")?
	(imports+=Import)*
	(elements+=JavaElement)*
;

Import:
	"import" identifier=DOT_ID";"
;

JavaElement:
	(annotations+=Annotation)*
	element=Annotatable
;

Annotatable:
	Method | Statement | JavaClass | JavaEnum
;

Annotation:
	"@" type=DOT_ID ("(" options+=AnnotationOption ("," options+=AnnotationOption)* ")")?
;

AnnotationOption:
	(param=DOT_ID "=")? (option=AnnotationOptionElement | annotation=Annotation | annotationArray=AnnotationArray)
;

AnnotationArray:
    "{" (annotations+=Annotation ("," annotations+=Annotation)*)? "}"
;

AnnotationOptionElement:
	DOT_ID | STRING
;

terminal DOT_ID: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'.'|'<'|'>')*;
terminal ARRAY_ID: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'.'|'<'|'>'|'['|']')*;

Statement:
	(modifiers+=Modifier)* type=DOT_ID name=DOT_ID ";"
;

JavaClass:
	(modifiers+=Modifier)* "class" name=DOT_ID  ("extends" extends=DOT_ID)? ("implements" implements=DOT_ID)?"{"
		(elements+=JavaElement)*
	"}"
;

JavaEnum:
	(modifiers+=Modifier)* "enum" name=DOT_ID "{"
		values+=DOT_ID ("," values+=DOT_ID)* ";"
		
		(elements+=JavaElement)*
	"}"
;

Method:
	(modifiers+=Modifier)* type=ID name=DOT_ID "(" (arguments+=Argument ("," arguments+=Argument)*)? ")" "{"
		(elements+=JavaElement)*
	"}"
;

Argument:
	argumentType=ARRAY_ID argumentName+=DOT_ID
;

enum Modifier: 
    Annotation = 'annotation' |
    public = 'public' |
    protected = 'protected' |
    private = 'private'|
    static = 'static'|
    abstract = 'abstract'|
    final = 'final'|
    native = 'native'|
    synchronized = 'synchronized'|
    transient = 'transient'|
    volatile = 'volatile'|
    strictfp  = 'strictfp'
    ;