grammar org.big.orm.OrmModel with org.eclipse.xtext.common.Terminals

generate ormModel "http://www.big.org/orm/OrmModel"

OrmModel:
	'orm_model' name=ID
	(elements+=ModelElement | relationships+=Relationship)*;

ModelElement:
	InheritableElement | Embeddable
;

InheritableElement:
	Entity | MappedClass
;

MappedClass:
	'mapped_class' name=ID ('extends' extends=[MappedClass])? '{'
		(attributes += DataAttribute)*
	'}'
;
	
Entity:
	(options+=EntityOption)*
	'entity' name=ID ('extends' extends=[InheritableElement])? '{'
		(attributes += Attribute)*
	'}';
	
Embeddable:
	'embeddable' name=ID '{'
		(attributes += Attribute)*
	'}';

Relationship:
	type=RelationshipType 'relationship' (unidirectional?='unidirectional')? name=ID '{'
		'source' source=RelationEntity (sourceRequired?='required')?
		'target' target=RelationEntity
		(attributes += Attribute)*
	'}';

RelationEntity:
	entity=[ModelElement] ('[' 
		(attributeName=STRING)?
	']')?;
	
Attribute:
	DataAttribute | EmbeddedAttribute
;

DataAttribute:
	name=ID (datatype=DataType) (type=AttributeType)?;
	
EmbeddedAttribute:
	name=ID (embeddedType=[Embeddable]) (type=AttributeType)?;

	
enum DataType:
	UUID = 'UUID' |
	STRING = 'String' |
	INT = 'Integer' |
	BOOLEAN = 'Boolean' 
	;
	
enum AttributeType:
	NONE = 'none' |
	ID = 'key' |
	REQUIRED = 'required';
	
enum RelationshipType:
	OneToOne = 'OneToOne' |
	OneToMany = 'OneToMany' |
	ManyToOne = 'ManyToOne' |
	ManyToMany = 'ManyToMany'
	;

EntityOption:
	InheritanceOption | TableNameOption
;

TableNameOption:
	'@(table_name=' name=ID ')'
;

InheritanceOption:
	'@('option=InheritanceStrategy')'
;
	
enum InheritanceStrategy:
	SINGLE_TABLE = 'Inheritance.SingleTable' |
	JOINED_TABLE = 'Inheritance.JoinedTable' |
	TABLE_PER_CLASS = 'Inheritance.TablePerClass'
;