grammar org.big.orm.OrmModel with org.eclipse.xtext.common.Terminals

generate ormModel "http://www.big.org/orm/OrmModel"

OrmModel:
	'orm_model' name=ID
	(elements+=ModelElement | relationships+=Relationship)*;

ModelElement:
	Entity | Embeddable
;
	
Entity:
	'entity' name=ID '{'
		(attributes += Attribute)*
	'}';
	
Embeddable:
	'embeddable' name=ID '{'
		(attributes += Attribute)*
	'}';

Relationship:
	type=RelationshipType 'relationship' (unidirectional?='unidirectional')? name=ID '{'
		'source' source=RelationEntity
		'target' target=RelationEntity
		(attributes += Attribute)*
	'}';

RelationEntity:
	entity=[ModelElement] ('[' 
		(targetName=STRING)?
	']')?;
	
Attribute:
	DataAttribute | EmbeddedAttribute
;

DataAttribute:
	name=ID (datatype=DataType) (type=AttributeType)?;
	
EmbeddedAttribute:
	name=ID (embeddedType=[Embeddable]) (type=AttributeType)?;
	
enum DataType:
	UUID = 'UUID' |
	STRING = 'String' |
	INT = 'Integer' |
	BOOLEAN = 'Boolean' 
	;
	
enum AttributeType:
	NONE = 'none' |
	ID = 'key' |
	OPTIONAL = 'optional';
	
enum RelationshipType:
	OneToOne = 'OneToOne' |
	OneToMany = 'OneToMany' |
	ManyToOne = 'ManyToOne' |
	ManyToMany = 'ManyToMany'
	;