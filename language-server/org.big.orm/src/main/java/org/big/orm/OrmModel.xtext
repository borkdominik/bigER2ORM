grammar org.big.orm.OrmModel with org.eclipse.xtext.common.Terminals

generate ormModel "http://www.big.org/orm/OrmModel"

OrmModel:
	'orm_model' name=ID
	(elements+=ModelElement | relationships+=Relationship)*;

ModelElement:
	AttributedElement | OrmEnum
;

AttributedElement:
	InheritableElement | Embeddable
;

InheritableElement:
	Entity | MappedClass
;

MappedClass:
	'mapped_class' name=ID ('extends' extends=[MappedClass])? '{'
		(attributes += DataAttribute)*
	'}'
;
	
Entity:
	(options+=EntityOption)*
	'entity' name=ID ('extends' extends=[InheritableElement])? (joinEntity?='joins' '(' joinSource=RelationEntity ',' joinTarget=RelationEntity ')')? '{'
		(attributes += Attribute)*
	'}';
	
Embeddable:
	'embeddable' name=ID '{'
		(attributes += Attribute)*
	'}';
	
OrmEnum:
	'enum' name=ID '{'
		(values += EnumValue)+
	'}';
	
EnumValue:
	value=UPPER_SNAKE
;

Relationship:
	type=RelationshipType 'relationship' (unidirectional?='unidirectional')? name=ID '{'
		'source' source=RelationEntity (sourceRequired?='required')?
		'target' target=RelationEntity
	'}';

RelationEntity:
	entity=[InheritableElement] ('[' 
		(attributeName=STRING)?
	']')?;
	
Attribute:
	DataAttribute | EmbeddedAttribute | EnumAttribute
;


DataAttribute:
	name=ID (datatype=DataType) (type=AttributeType)?;
	
EmbeddedAttribute:
	'embedded' name=ID (embeddedType=[Embeddable]) (type=AttributeType)?;
	
EnumAttribute:
	'enum' name=ID (enumType=[OrmEnum]) (type=AttributeType)?;

	
enum DataType:
	UUID = 'UUID' |
	STRING = 'String' |
	INT = 'Integer' |
	BOOLEAN = 'Boolean' 
	;
	
enum AttributeType:
	NONE = 'none' |
	ID = 'key' |
	REQUIRED = 'required';
	
enum RelationshipType:
	OneToOne = 'OneToOne' |
	ManyToOne = 'ManyToOne' |
	ManyToMany = 'ManyToMany'
	;

EntityOption:
	InheritanceOption | TableNameOption
;

TableNameOption:
	'@(table_name=' name=ID ')'
;

InheritanceOption:
	'@('option=InheritanceStrategy')'
;
	
enum InheritanceStrategy:
	UNDEFINED = 'Inheritance.Undefined' |
	JOINED_TABLE = 'Inheritance.JoinedTable' |
	SINGLE_TABLE = 'Inheritance.SingleTable' |
	TABLE_PER_CLASS = 'Inheritance.TablePerClass'
;

terminal UPPER_SNAKE: '^'?('A'..'Z')+ ('_' ('A'..'Z')+)* ;