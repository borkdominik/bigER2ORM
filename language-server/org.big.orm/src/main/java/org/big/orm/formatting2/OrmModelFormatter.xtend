/*
 * generated by Xtext 2.28.0
 */
package org.big.orm.formatting2

import org.big.orm.ormModel.OrmModel
import org.eclipse.xtext.formatting2.AbstractFormatter2
import org.eclipse.xtext.formatting2.IFormattableDocument

import static org.big.orm.ormModel.OrmModelPackage.Literals.*
import org.big.orm.ormModel.ModelElement
import org.big.orm.ormModel.Relationship
import org.big.orm.ormModel.AttributedElement
import org.big.orm.ormModel.OrmEnum
import org.eclipse.xtext.formatting2.FormatterPreferenceKeys
import org.eclipse.xtext.preferences.MapBasedPreferenceValues
import java.util.LinkedHashMap
import org.big.orm.ormModel.RelationEntity
import org.big.orm.ormModel.EntityOption

class OrmModelFormatter extends AbstractFormatter2 {
	
	// @Inject extension OrmModelGrammarAccess

	def dispatch void format(OrmModel ormModel, extension IFormattableDocument document) {
		
		System.err.println("Formatter triggered!")
		
		// Model header
		ormModel.regionFor.feature(ORM_MODEL__NAME).surround[oneSpace]
		ormModel.regionFor.feature(ORM_MODEL__NAME).append[setNewLines(2, 2, 2)]
		
		for (modelElement : ormModel.elements) {
			modelElement.format
		}
		
		for (relationship : ormModel.relationships) {
			relationship.format
		}
		
		// use spaces instead of tabs
		val preferences = getPreferences
		val newMap = new LinkedHashMap<String, String>
		newMap.put(FormatterPreferenceKeys.indentation.id, '    ')
		val result = new MapBasedPreferenceValues(preferences, newMap)
		request.preferences = result
	}

	def dispatch void format(ModelElement element, extension IFormattableDocument document) {
		element.regionFor.feature(MODEL_ELEMENT__NAME).surround[oneSpace]
		// element.regionFor.feature(ENTITY__OPTIONS).surround[newLine]
		// element.regionFor.feature(ENTITY__JOIN_SOURCE).surround[noSpace]
		// element.regionFor.feature(ENTITY__JOIN_TARGET).surround[noSpace]
		
		val open = element.regionFor.keyword("{")
		val close = element.regionFor.keyword("}")
		
		if  (element instanceof AttributedElement) {
			if (element.attributes.length > 0) {
				open.append[newLine]
			}
		
			for (attribute : element.attributes) {
				attribute.append[setNewLines(1, 1, 2)]
			}
		} else if (element instanceof OrmEnum) {
			open.append[newLine]
			
			for (value : element.values) {
				value.append[setNewLines(1, 1, 2)]
			}
		}

		
		interior(open, close)[indent]
		close.append[setNewLines(2,2,2)]
	}
	
	def dispatch void format(EntityOption element, extension IFormattableDocument document) {
		element.append[newLine]
	}
	
	def dispatch void format(RelationEntity element, extension IFormattableDocument document) {
		element.regionFor.feature(RELATION_ENTITY__ENTITY).surround[noSpace]
		element.regionFor.feature(RELATION_ENTITY__ATTRIBUTE_NAME).surround[noSpace]
	}
	
	def dispatch void format(Relationship relationship, extension IFormattableDocument document) {
		relationship.regionFor.feature(RELATIONSHIP__NAME).surround[oneSpace]
		relationship.regionFor.feature(RELATIONSHIP__TYPE).surround[oneSpace]
		relationship.regionFor.feature(RELATIONSHIP__UNIDIRECTIONAL).surround[oneSpace]
		
		val open = relationship.regionFor.keyword("{")
		val close = relationship.regionFor.keyword("}")
		open.append[newLine]
		interior(open, close)[indent]
		close.append[setNewLines(2,2,2)]
		
		relationship.source.append[setNewLines(1, 1, 2)]
		relationship.source.regionFor.feature(RELATION_ENTITY__ATTRIBUTE_NAME).surround[noSpace]
		relationship.target.append[setNewLines(1, 1, 2)]
		relationship.target.regionFor.feature(RELATION_ENTITY__ATTRIBUTE_NAME).surround[noSpace]
	}
	
}